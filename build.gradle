buildscript {
    ext.kotlin_version = '1.0.3'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

group 'MicroUtils'
version '1.3.2'




apply plugin: 'kotlin'
// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
sourceCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.21'
    testCompile 'junit:junit:4.12'
}

//https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = 'oshai'
    key = 'my_key' //https://bintray.com/profile/edit
    publications = ['MyPublication']
    pkg {
        repo = 'kotlin-logging'
        name = 'kotlin-logging'
        userOrg = 'microutils'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/MicroUtils/kotlin-logging'
        version {
            name = '1.3.2'
            desc = 'kotlin-logging 1.3.2 - Lightweight logging framework for Kotlin'
            released  = new Date()
        }
    }
}
// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'io.github.microutils'
            artifactId 'kotlin-logging'
            version '1.3.2'

            artifact sourcesJar
            artifact javadocJar
        }
    }
}
